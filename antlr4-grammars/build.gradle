apply plugin: 'java-project'

defaultTasks 'clean', 'test'

configurations {
	antlr4
	compile.extendsFrom antlr4
}

dependencies {
	antlr4 group: 'org.antlr', name: 'antlr4', version: '4.3'
}

new File("${projectDir}/meta").eachFile(groovy.io.FileType.FILES) {
	Properties prop = new Properties()
	prop.load(it.newInputStream())
	
	task "transpile${prop.grammarName}" (type: com.github.appocalypse.build.antlr4.Antlr4Transpile) {
		antlr4Classpath = configurations.antlr4
		source = "${projectDir}/${prop.antlr4Source}"
		javaPackage = prop.javaPackage
	}
	
	if (prop.testSource) {
		task "test${prop.grammarName}" (type: com.github.appocalypse.build.antlr4.Antlr4Test, dependsOn: 'compileJava') {
			antlr4Classpath = sourceSets.main.runtimeClasspath
			source = "${projectDir}/${prop.testSource}"
			javaPackage = prop.javaPackage
			grammarName = prop.grammarName
			entryPoint = prop.entryPoint
		}
	}
}

compileJava {
	source = tasks.withType(com.github.appocalypse.build.antlr4.Antlr4Transpile)
}

test.dependsOn tasks.withType(com.github.appocalypse.build.antlr4.Antlr4Test)